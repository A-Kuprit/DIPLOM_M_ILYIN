#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		НовоеУведомление = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();  
		НовоеУведомление.ТекстСообщения = СтрШаблон("Создан новый документ. Исполнитель %1. Дата %2 интервал работ %3 - %4. ОписаниеПроблемы - %5", 
		Специалист, Формат(ДатаПроведенияРабот, "ДЛФ=D"), Формат(ВремяНачалаРабот, "ДЛФ=T"), Формат(ВремяОкончанияРабот, "ДЛФ=T"), ОписаниеПроблемы);
		НовоеУведомление.Записать();		
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК СпециалистДок,
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРаботДок,
		|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРабот КАК ВремяНачалаРаботДок,
		|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРабот КАК ВремяОкончанияРаботДок,
		|	ВКМ_ОбслуживаниеКлиента.ОписаниеПроблемы КАК ОписаниеПроблемыДок,
		|	ВКМ_ОбслуживаниеКлиента.Комментарий КАК КомментарийДок
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда 
			
			Если Специалист <> Выборка.СпециалистДок Тогда
				НовоеУведомление = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
				НовоеУведомление.ТекстСообщения = СтрШаблон("В документе %1 изменился специалист. Вместо %2, назначен %3.", 
					Ссылка, Выборка.СпециалистДок, Специалист);
				НовоеУведомление.Записать();
			КонецЕсли;    
			
			Если ДатаПроведенияРабот <> Выборка.ДатаПроведенияРаботДок Тогда
				НовоеУведомление = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
				НовоеУведомление.ТекстСообщения = СтрШаблон("В документе %1 изменилась дата проведения работ. Работы перенесены с %2 на %3.", 
					Ссылка, Формат(Выборка.ДатаПроведенияРаботДок, "ДЛФ=D"), Формат(ДатаПроведенияРабот, "ДЛФ=D"));
				НовоеУведомление.Записать();
			КонецЕсли;
			
			Если ВремяНачалаРабот <> Выборка.ВремяНачалаРаботДок Или ВремяОкончанияРабот <> Выборка.ВремяОкончанияРаботДок Тогда
				НовоеУведомление = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
				НовоеУведомление.ТекстСообщения = СтрШаблон("В документе %1 изменилось плановое время работ, новый интервал %2 - %3.", 
					Ссылка, Формат(ВремяНачалаРабот, "ДЛФ=T"), Формат(ВремяОкончанияРабот, "ДЛФ=T"));
				НовоеУведомление.Записать();
			КонецЕсли;
			
			Если ОписаниеПроблемы <> Выборка.ОписаниеПроблемыДок Или Комментарий <> Выборка.КомментарийДок Тогда
				НовоеУведомление = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
				НовоеУведомление.ТекстСообщения = СтрШаблон("В документ %1 добавлена информация %2%3", 
					Ссылка, ОписаниеПроблемы, Комментарий);
				НовоеУведомление.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры  

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ПериодДействияС КАК ДатаНачала,
	|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ПериодДействияПо КАК ДатаОкончания,
	|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СуммаАбонентскойПлаты КАК СуммаАбонентскойПлаты,
	|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
	|	ВКМ_ОбслуживаниеКлиента.Договор.Представление КАК Договор,
	|	ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора КАК ВидДоговора,
	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
	|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&МоментВремени, ) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВКМ_ОбслуживаниеКлиента.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка
	|	И ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора = &ВидДоговора";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		
		Если ПустаяСтрока(Выборка.ПроцентОтРабот) Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Специалисту: %1 не установлен 'Процент оплаты от работ'. Проведение документа невозможно. ", Выборка.Специалист));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		// регистр ВКМ_ВыполненныеСотрудникомРаботы   
		Движение              = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период       = Дата;
		Движение.Сотрудник    = Специалист;
		Движение.ЧасовКОплате = ВыполненныеРаботы.Итог("ЧасыКОплатеКлиенту");
		Движение.СуммаКОплате = Движение.ЧасовКОплате * Выборка.СтоимостьЧасаРаботы * Выборка.ПроцентОтРабот / 100;
		
		// регистр ВКМ_ВыполненныеКлиентуРаботы 
		Движение                 = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период          = Дата;
		Движение.Клиент          = Клиент;
		Движение.Договор         = Договор;
		Движение.КоличествоЧасов = ВыполненныеРаботы.Итог("ЧасыКОплатеКлиенту");
		Движение.СуммаКОплате    = Движение.КоличествоЧасов * Выборка.СтоимостьЧасаРаботы;
		
	Иначе 
		
		ОбщегоНазначения.СообщитьПользователю("Выбранный договор не является договором с абонентской платой или срок его действия окончен. Выберите актуальный договор");
		Отказ = Истина;
		Возврат; 
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли
