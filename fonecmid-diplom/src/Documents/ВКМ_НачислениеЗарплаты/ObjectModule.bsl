#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры 

Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина; 
	Движения.ВКМ_Удержания.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	СформироватьДвижения();
	
	РассчитатьОклад();	
	РассчитьтьОтпуск();
	РассчитатьНДФЛ();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Процедура СформироватьДвижения() 
	
	СформироватьДвиженияПоОкладу();  
	СформироватьДвиженияПрочиеОсновныеНачисления();
	СформироватьДвиженияДополнительныеНачисления(); 
	СформироватьДвижения_Удержания();
	СформироватьДвиженияВзаиморасчетыССотрудниками();
    			
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();   
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать(); 
    			
КонецПроцедуры

Процедура СформироватьДвиженияПоОкладу() 
	
	Отбор = Новый Структура();
	Отбор.Вставить("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад); 
	
	СтрокиОклада = Начисления.НайтиСтроки(Отбор);
	
	Если СтрокиОклада.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МинимальнаяДатаНачала = СтрокиОклада[0].ДатаНачала;
	МаксимальнаяДатаОкончания = СтрокиОклада[0].ДатаОкончания;	
	
	Для Счет = 1 по СтрокиОклада.Количество() - 1 Цикл
		
		Если МинимальнаяДатаНачала > СтрокиОклада[Счет].ДатаНачала Тогда
			МинимальнаяДатаНачала = СтрокиОклада[Счет].ДатаНачала;
		КонецЕсли;
		
		Если МаксимальнаяДатаОкончания < СтрокиОклада[Счет].ДатаОкончания Тогда
			МаксимальнаяДатаОкончания = СтрокиОклада[Счет].ДатаОкончания;
		КонецЕсли;
		
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗарплатыНачисления.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВТ_ПериодыОкладаИсходные
		|ИЗ
		|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &Оклад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Период КАК Период,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад
		|ПОМЕСТИТЬ ВТ_ИзмененияОклада
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(
		|			&МинимальнаяДатаНачала,
		|			Сотрудник В
		|				(ВЫБРАТЬ
		|					ВТ_ПериодыОкладаИсходные.Сотрудник КАК Сотрудник
		|				ИЗ
		|					ВТ_ПериодыОкладаИсходные КАК ВТ_ПериодыОкладаИсходные)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудников.Период,
		|	ВКМ_УсловияОплатыСотрудников.Сотрудник,
		|	ВКМ_УсловияОплатыСотрудников.Оклад
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		|ГДЕ
		|	ВКМ_УсловияОплатыСотрудников.Период > &МинимальнаяДатаНачала
		|	И ВКМ_УсловияОплатыСотрудников.Период <= &МаксимальнаяДатаОкончания
		|	И ВКМ_УсловияОплатыСотрудников.Сотрудник В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВТ_ПериодыОкладаИсходные.Сотрудник КАК Сотрудник
		|			ИЗ
		|				ВТ_ПериодыОкладаИсходные КАК ВТ_ПериодыОкладаИсходные)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатаНачала.Сотрудник КАК Сотрудник,
		|	ДатаНачала.Период КАК ДатаНачала,
		|	МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ДатаОкончания.Период, СЕКУНДА, -1), ДАТАВРЕМЯ(3999, 12, 31))) КАК ДатаОкончания,
		|	ДатаНачала.Оклад КАК Оклад
		|ПОМЕСТИТЬ ВТ_ИнтервалОклада
		|ИЗ
		|	ВТ_ИзмененияОклада КАК ДатаНачала
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзмененияОклада КАК ДатаОкончания
		|		ПО ДатаНачала.Сотрудник = ДатаОкончания.Сотрудник
		|			И ДатаНачала.Период < ДатаОкончания.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатаНачала.Оклад,
		|	ДатаНачала.Период,
		|	ДатаНачала.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПериодыОкладаИсходные.Сотрудник КАК Сотрудник,
		|	ВТ_ПериодыОкладаИсходные.ВидРасчета КАК ВидРасчета,
		|	ВТ_ПериодыОкладаИсходные.ГрафикРаботы КАК ГрафикРаботы,
		|	ВТ_ИнтервалОклада.Оклад КАК Размер,
		|	ВЫБОР
		|		КОГДА ВТ_ИнтервалОклада.ДатаНачала > ВТ_ПериодыОкладаИсходные.ДатаНачала
		|			ТОГДА ВТ_ИнтервалОклада.ДатаНачала
		|		ИНАЧЕ ВТ_ПериодыОкладаИсходные.ДатаНачала
		|	КОНЕЦ КАК ПериодДействияНачало,
		|	ВЫБОР
		|		КОГДА ВТ_ИнтервалОклада.ДатаОкончания < ВТ_ПериодыОкладаИсходные.ДатаОкончания
		|			ТОГДА ВТ_ИнтервалОклада.ДатаОкончания
		|		ИНАЧЕ ВТ_ПериодыОкладаИсходные.ДатаОкончания
		|	КОНЕЦ КАК ПериодДействияКонец
		|ИЗ
		|	ВТ_ПериодыОкладаИсходные КАК ВТ_ПериодыОкладаИсходные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнтервалОклада КАК ВТ_ИнтервалОклада
		|		ПО ВТ_ПериодыОкладаИсходные.Сотрудник = ВТ_ИнтервалОклада.Сотрудник
		|			И (ВТ_ИнтервалОклада.ДатаНачала <= ВТ_ПериодыОкладаИсходные.ДатаОкончания)
		|			И ВТ_ПериодыОкладаИсходные.ДатаНачала <= ВТ_ИнтервалОклада.ДатаОкончания";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
    Запрос.УстановитьПараметр("МинимальнаяДатаНачала", МинимальнаяДатаНачала);
	Запрос.УстановитьПараметр("МаксимальнаяДатаОкончания", МаксимальнаяДатаОкончания);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		//Регистр расчета ОсновныеНачислени
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.ПериодРегистрации = Дата; 
												
	КонецЦикла; 	
		
КонецПроцедуры   

Процедура СформироватьДвиженияПрочиеОсновныеНачисления()
	
	Для Каждого Строка Из Начисления Цикл    
		
		Если ТипЗнч(Строка.ВидРасчета) <> Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
			Продолжить;
		КонецЕсли;
		
		//ПрочиеОсновныеНачисления		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;		
		Движение.ПериодДействияНачало = Строка.ДатаНачала;
		Движение.ПериодДействияКонец = Строка.ДатаОкончания;
		Движение.ВидРасчета = Строка.ВидРасчета;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.ГрафикРаботы = Строка.ГрафикРаботы; 
		
		Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Строка.ДатаОкончания, -1)); 	
		КонецЕсли;
												
	КонецЦикла;
	
КонецПроцедуры   

Процедура СформироватьДвиженияДополнительныеНачисления()
	
	Отбор = Новый Структура();
	Отбор.Вставить("ВидРасчета", ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПроцентОтРабот); 
	
	СтрокиПроцентОтРабот = Начисления.НайтиСтроки(Отбор);
	
	Если СтрокиПроцентОтРабот.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МинимальнаяДатаНачала = СтрокиПроцентОтРабот[0].ДатаНачала;
	МаксимальнаяДатаОкончания = СтрокиПроцентОтРабот[0].ДатаОкончания;
	
	Для Счет = 1 по СтрокиПроцентОтРабот.Количество() - 1 Цикл
		
		Если МинимальнаяДатаНачала > СтрокиПроцентОтРабот[Счет].ДатаНачала Тогда
			МинимальнаяДатаНачала = СтрокиПроцентОтРабот[Счет].ДатаНачала;
		КонецЕсли;
		
		Если МаксимальнаяДатаОкончания < СтрокиПроцентОтРабот[Счет].ДатаОкончания Тогда
			МаксимальнаяДатаОкончания = СтрокиПроцентОтРабот[Счет].ДатаОкончания;
		КонецЕсли;
		
	КонецЦикла;  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТ_Док
		|ИЗ
		|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Док.Сотрудник КАК Сотрудник,
		|	ВТ_Док.ВидРасчета КАК ВидРасчета,
		|	ВТ_Док.ДатаНачала КАК ПериодДействияНачало,
		|	ВТ_Док.ДатаОкончания КАК ПериодДействияКонец,
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот КАК Результат
		|ИЗ
		|	ВТ_Док КАК ВТ_Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(
		|				&ДатаНачала,
		|				&ДатаОкончания,
		|				,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						ВТ_Док.Сотрудник КАК Сотрудник
		|					ИЗ
		|						ВТ_Док КАК ВТ_Док)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ПО ВТ_Док.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПроцентОтРабот);
	Запрос.УстановитьПараметр("ДатаНачала", МинимальнаяДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", МаксимальнаяДатаОкончания);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		//Регистр расчета ДополнительныеНачисления		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.ПериодРегистрации = Дата; 
						
	КонецЦикла;	 
	
	Движения.ВКМ_ДополнительныеНачисления.Записать(); 
	    	
КонецПроцедуры 

Процедура СформироватьДвижения_Удержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		//Регистр расчета Удержания	
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = НачалоМесяца(Дата);
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		
	КонецЦикла;  
		
КонецПроцедуры

Процедура СформироватьДвиженияВзаиморасчетыССотрудниками()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_Сотрудники
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВКМ_ОсновныеНачисления.Результат) КАК ОснНачисления,
	|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_ОснНачисления
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_ДополнительныеНачисления.Результат) КАК ДопНачисления
	|ПОМЕСТИТЬ ВТ_ДопНачисления
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ДополнительныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_Удержания.Результат) КАК Удержания
	|ПОМЕСТИТЬ ВТ_Удержания
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_Удержания.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сотрудники.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВТ_ДопНачисления.ДопНачисления, 0) КАК ДопНачисления,
	|	ЕСТЬNULL(ВТ_ОснНачисления.ОснНачисления, 0) КАК ОснНачисления,
	|	ЕСТЬNULL(ВТ_Удержания.Удержания, 0) КАК Удержания,
	|	ЕСТЬNULL(ВТ_ДопНачисления.ДопНачисления, 0) + ЕСТЬNULL(ВТ_ОснНачисления.ОснНачисления, 0) - ЕСТЬNULL(ВТ_Удержания.Удержания, 0) КАК Сумма
	|ИЗ
	|	ВТ_Сотрудники КАК ВТ_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДопНачисления КАК ВТ_ДопНачисления
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_ДопНачисления.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОснНачисления КАК ВТ_ОснНачисления
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_ОснНачисления.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Удержания КАК ВТ_Удержания
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_Удержания.Сотрудник"; 
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи =  РезультатЗапроса.Выбрать(); 
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить(); 
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход; 
		Движение.Период = Дата;  
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;   
		 
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();	
	
КонецПроцедуры  

Процедура РассчитатьОклад() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК Норма
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			ВидРасчета = &Оклад
	|				И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
		
		Если ВыборкаДетальныеЗаписи.Норма = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю("Выбранный график работ не заполнен ! Проведение не возможно!");
		КонецЕсли;
		
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1]; 
		Начислено = Движение.Размер * ВыборкаДетальныеЗаписи.Факт / ВыборкаДетальныеЗаписи.Норма;
		Движение.Результат = Начислено;
		Движение.ОтработаноЧасов = ВыборкаДетальныеЗаписи.Факт;	
				
	КонецЦикла;   
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
		
КонецПроцедуры

Процедура РассчитьтьОтпуск() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК БазаДоп,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК БазаОсн,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОтработаноЧасовБаза, 0) КАК БазаОтработано,
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияКонец КАК ПериодДействияКонец
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Ссылка
		|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Ссылка
		|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
		|				Регистратор = &Ссылка
		|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск";  
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
		
	Измерения = Новый Массив; 
	Измерения.Добавить("Сотрудник");
    Запрос.УстановитьПараметр("Измерения", Измерения);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		Движение.ОтработаноЧасов = ВыборкаДетальныеЗаписи.БазаОтработано; 
		
		Если ВыборкаДетальныеЗаписи.БазаОтработано = 0 Тогда
			Движение.Результат = 0;
			Продолжить;
		КонецЕсли; 
		
		КалендарныхДнейОтпуска = (ВыборкаДетальныеЗаписи.ПериодДействияКонец - ВыборкаДетальныеЗаписи.ПериодДействияНачало)/ (24*60*60)+1;
		КоличествоРабочихДней = ВыборкаДетальныеЗаписи.БазаОтработано / 8;
		
		Движение.Размер = (ВыборкаДетальныеЗаписи.БазаОсн + ВыборкаДетальныеЗаписи.БазаДоп);
		Начислено = (ВыборкаДетальныеЗаписи.БазаОсн + ВыборкаДетальныеЗаписи.БазаДоп) / КоличествоРабочихДней * КалендарныхДнейОтпуска;
		Движение.Результат = Начислено;
						
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина); 
		
КонецПроцедуры

Процедура РассчитатьНДФЛ() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	|	ВКМ_Удержания.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК РезультатБазаОсновные
	|ПОМЕСТИТЬ ВТ_УдержанияОсновные
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(
	|				&Измерение,
	|				&Измерение,
	|				,
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
	|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_УдержанияОсновные.НомерСтроки КАК НомерСтроки,
	|	ВТ_УдержанияОсновные.РезультатБазаОсновные КАК РезультатБазаОсновные,
	|	ЕСТЬNULL(ВКМ_ДополнительныеНачисления.Результат, 0) КАК РезультатДополнительные
	|ИЗ
	|	ВТ_УдержанияОсновные КАК ВТ_УдержанияОсновные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|		ПО ВТ_УдержанияОсновные.Сотрудник = ВКМ_ДополнительныеНачисления.Сотрудник
	|			И ВТ_УдержанияОсновные.Регистратор = ВКМ_ДополнительныеНачисления.Регистратор";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	
	Измерение = Новый Массив;
	Измерение.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерение", Измерение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		РезультатБазаОбщая = Выборка.РезультатБазаОсновные + Выборка.РезультатДополнительные;
		Движение.Размер = РезультатБазаОбщая;
		Движение.Результат = РезультатБазаОбщая * 13 / 100;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать(, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
