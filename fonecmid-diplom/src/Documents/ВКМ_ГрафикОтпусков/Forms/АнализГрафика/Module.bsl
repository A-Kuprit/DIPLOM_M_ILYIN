#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	    	
	//@skip-check unknown-form-parameter-access
	ДанныеДокумента = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	
	Если НЕ ДанныеДокумента.ПоДокументу.Проведен Тогда
		ОбщегоНазначения.СообщитьПользователю("График выводится только по проведенному документу!!!");
		Возврат;
	КонецЕсли;	
	
	ОтпускаСотрудников = ДанныеОтпускаСотрудников(ДанныеДокумента.ПоДокументу);
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Нет;
	Шкала = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы.Вставить(0); 
	Шкала.Метки.Очистить();
	
	МеткаВремени = Шкала.Метки.Добавить(НачалоДня(ТекущаяДатаСеанса()));
	МеткаВремени.Текст      = "Текущая дата " + Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy");
	МеткаВремени.ЦветТекста = WebЦвета.Красный;				
	МеткаВремени.ЦветЛинии  = WebЦвета.Красный;
	
	Серия = Диаграмма.УстановитьСерию("Отпуск");
	
	Пока ОтпускаСотрудников.Следующий() Цикл
		
		Точка       = Диаграмма.УстановитьТочку(ОтпускаСотрудников.Сотрудник);
		Точка.Текст = СокрЛП(ОтпускаСотрудников.СотрудникПредставление);
		
		Значение    = Диаграмма.ПолучитьЗначение(Точка, Серия);		
		Интервал    = Значение.Добавить();
		
		Интервал.Начало = ОтпускаСотрудников.ДатаНачала;
		Интервал.Конец  = ОтпускаСотрудников.ДатаОкончания;
		
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеОтпускаСотрудников(ПоДокументу)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусков.Сотрудник КАК Сотрудник,
		|	ВКМ_ГрафикОтпусков.Сотрудник.Представление КАК СотрудникПредставление,
		|	ВКМ_ГрафикОтпусков.ДатаНачала КАК ДатаНачала,
		|	ВКМ_ГрафикОтпусков.ДатаОкончания КАК ДатаОкончания
		|ИЗ
		|	РегистрСведений.ВКМ_ГрафикОтпусков КАК ВКМ_ГрафикОтпусков
		|ГДЕ
		|	ВКМ_ГрафикОтпусков.Регистратор = &ПоДокументу";
	
	Запрос.УстановитьПараметр("ПоДокументу", ПоДокументу);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи;
	
КонецФункции

#КонецОбласти
